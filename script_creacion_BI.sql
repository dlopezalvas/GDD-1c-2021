USE [GD1C2021]
GO

CREATE TABLE [BREAKFAST_CLUB].[BI_TIEMPO] (
  [COD_TIEMPO] decimal(18,0) IDENTITY (1,1),
  [ANIO] int,
  [MES] int,
  PRIMARY KEY ([COD_TIEMPO]),
  CONSTRAINT ck_tiempo_mes CHECK ([MES] BETWEEN 0 AND 12) 
);
/*
CREATE TABLE [BREAKFAST_CLUB].[BI_CLIENTE] (
  [COD_CLIENTE] decimal(18,0) NOT NULL,
  [NOMBRE] nvarchar(255) NOT NULL,
  [APELLIDO] nvarchar(255) NOT NULL,
  [DNI] decimal(18,0) NOT NULL,
  [EDAD] nvarchar(50) NOT NULL,
  [MAIL] nvarchar(255) ,
  [TELEFONO] int ,
  [DIRECCION] nvarchar(255),
  PRIMARY KEY ([COD_CLIENTE]),
  CONSTRAINT ck_cliente_edad CHECK ([EDAD] IN ('18-30anios','31-50anios', '>50anios'))
);
*/
CREATE TABLE [BREAKFAST_CLUB].[BI_SUCURSAL](
	[COD_SUCURSAL] decimal (18,0),
	[COD_CIUDAD] decimal (18,0) REFERENCES [BREAKFAST_CLUB].CIUDAD,
	[MAIL] nvarchar(255),
	[TELEFONO] decimal (18,0),
	[DIRECCION] nvarchar(255),
	CONSTRAINT COD_BI_SUCURSAL_PK PRIMARY KEY ([COD_SUCURSAL])
);

CREATE TABLE [BREAKFAST_CLUB].[BI_PC] (
    [COD_PC] nvarchar(50),
    [ALTO] decimal(18,2),
    [ANCHO] decimal(18,2),
    [PROFUNDIDAD] decimal(18,2),
	[TIEMPO_PROMEDIO_EN_STOCK] decimal(18,2),
	[PRECIO_PROMEDIO_VENTA] decimal(18,2),
	[PRECIO_PROMEDIO_COMPRA] decimal(18,2),
    CONSTRAINT COD_BI_PC_PK PRIMARY KEY ([COD_PC])
);

CREATE TABLE [BREAKFAST_CLUB].[BI_FABRICANTE](
	[COD_FABRICANTE] decimal (18,0),
	[NOMBRE] nvarchar(255),
	CONSTRAINT FABRICANTE_BI_PK PRIMARY KEY ([COD_FABRICANTE])
);

CREATE TABLE [BREAKFAST_CLUB].[BI_GPU](
    [COD_GPU] decimal (18,0),
    [CAPACIDAD] nvarchar(255),
    [MODELO] nvarchar(255),
	[COD_FABRICANTE] decimal(18,0) REFERENCES [BREAKFAST_CLUB].[BI_FABRICANTE],
    [CHIPSET] nvarchar(50),
    [VELOCIDAD] nvarchar(50),
    CONSTRAINT GPU_BI_PK PRIMARY KEY ([COD_GPU])
);

CREATE TABLE [BREAKFAST_CLUB].[BI_CPU] (
    [COD_CPU] nvarchar(50),
    [CACHE] nvarchar(50),
    [CANT_HILOS] nvarchar(50),
	[COD_FABRICANTE] decimal(18,0) REFERENCES [BREAKFAST_CLUB].[BI_FABRICANTE],
    [VELOCIDAD] varchar(50),
    CONSTRAINT CPU_BI_PK PRIMARY KEY ([COD_CPU])
);

CREATE TABLE [BREAKFAST_CLUB].[BI_DISCO_RIGIDO] (
    [COD_DISCO] nvarchar(255),
    [TIPO] nvarchar(255),
    [CAPACIDAD] nvarchar(255),
	[COD_FABRICANTE] decimal(18,0) REFERENCES [BREAKFAST_CLUB].[BI_FABRICANTE],
    [VELOCIDAD] nvarchar(255),
    CONSTRAINT DISCO_RIGIDO_BI_PK PRIMARY KEY ([COD_DISCO])
);

CREATE TABLE [BREAKFAST_CLUB].[BI_RAM] (
    [COD_RAM] nvarchar(255),
    [TIPO] nvarchar(255),
    [CAPACIDAD] nvarchar(255),
	[COD_FABRICANTE] decimal(18,0) REFERENCES [BREAKFAST_CLUB].[BI_FABRICANTE],
    [VELOCIDAD] nvarchar(255),
    CONSTRAINT COD_RAM_BI_PK PRIMARY KEY ([COD_RAM])
);

CREATE TABLE [BREAKFAST_CLUB].[BI_ACCESORIO] (
   [COD_ACCES] decimal(18,0),
   [DESCRIPCION] nvarchar(255),
   [TIEMPO_PROMEDIO_EN_STOCK] decimal(18,2),
   [PRECIO_PROMEDIO_VENTA] decimal(18,2),
   [PRECIO_PROMEDIO_COMPRA] decimal(18,2),
   CONSTRAINT cod_accesorio_BI_pk PRIMARY KEY ([COD_ACCES])
);

CREATE TABLE [BREAKFAST_CLUB].[BI_HECHOS_VENTAS_PC] (
  [COD_PC] nvarchar(50) REFERENCES [BREAKFAST_CLUB].[BI_PC],
  [COD_RAM] nvarchar(255) REFERENCES [BREAKFAST_CLUB].[BI_RAM],
  [COD_DISCO] nvarchar(255) REFERENCES [BREAKFAST_CLUB].[BI_DISCO_RIGIDO],
  [COD_GPU] decimal (18,0) REFERENCES [BREAKFAST_CLUB].[BI_GPU],
  [COD_CPU] nvarchar(50) REFERENCES [BREAKFAST_CLUB].[BI_CPU],
  [COD_SUCURSAL] decimal (18,0) REFERENCES [BREAKFAST_CLUB].[BI_SUCURSAL],
  [EDAD] nvarchar(50),
  [COD_TIEMPO] decimal(18,0) REFERENCES [BREAKFAST_CLUB].[BI_TIEMPO],
  [PRECIO] decimal(18,2) ,
  [CANTIDAD_VENDIDA] decimal(18,0),
  [GANANCIAS_MES_SUC] decimal(18,0),
  [TOTAL_VENDIDO_MES_SUC_PC] decimal(18,0),
  CONSTRAINT ck_cliente_edad_pc CHECK ([EDAD] IN ('18-30anios','31-50anios', '>50anios'))
);

CREATE TABLE [BREAKFAST_CLUB].[BI_HECHOS_COMPRAS_PC] (
  [COD_PC] nvarchar(50) REFERENCES [BREAKFAST_CLUB].[BI_PC],
  [COD_RAM] nvarchar(255) REFERENCES [BREAKFAST_CLUB].[BI_RAM],
  [COD_DISCO] nvarchar(255) REFERENCES [BREAKFAST_CLUB].[BI_DISCO_RIGIDO],
  [COD_GPU] decimal (18,0) REFERENCES [BREAKFAST_CLUB].[BI_GPU],
  [COD_CPU] nvarchar(50) REFERENCES [BREAKFAST_CLUB].[BI_CPU],
  [COD_SUCURSAL] decimal (18,0) REFERENCES [BREAKFAST_CLUB].[BI_SUCURSAL],
  [COD_TIEMPO] decimal(18,0) REFERENCES [BREAKFAST_CLUB].[BI_TIEMPO],
  [PRECIO] decimal(18,2) ,
  [CANTIDAD_COMPRADA] decimal(18,0),
  [GANANCIAS_MES_SUC] decimal(18,0),
);

CREATE TABLE [BREAKFAST_CLUB].[BI_HECHOS_VENTAS_ACCESORIOS] (
  [COD_ACCESORIO] decimal(18,0) REFERENCES [BREAKFAST_CLUB].[BI_ACCESORIO],
  [COD_SUCURSAL] decimal (18,0) REFERENCES [BREAKFAST_CLUB].[BI_SUCURSAL],
  [COD_TIEMPO] decimal(18,0) REFERENCES [BREAKFAST_CLUB].[BI_TIEMPO],
  [PRECIO] decimal(18,2) ,
  [CANTIDAD_VENDIDA] decimal(18,0),
  [EDAD] nvarchar(50),
  [MAX_STOCK_ANUAL_SUCURSAL] decimal(18,0),
  [GANANCIAS_MES_SUC] decimal(18,0),
  [TOTAL_VENDIDO_MES_SUC_ACC] decimal(18,0),
  CONSTRAINT ck_cliente_edad_acc CHECK ([EDAD] IN ('18-30anios','31-50anios', '>50anios'))
);

CREATE TABLE [BREAKFAST_CLUB].[BI_HECHOS_COMPRAS_ACCESORIOS] (
  [COD_ACCESORIO] decimal(18,0) REFERENCES [BREAKFAST_CLUB].[BI_ACCESORIO],
  [COD_SUCURSAL] decimal (18,0) REFERENCES [BREAKFAST_CLUB].[BI_SUCURSAL],
  [COD_TIEMPO] decimal(18,0) REFERENCES [BREAKFAST_CLUB].[BI_TIEMPO],
  [PRECIO] decimal(18,2) ,
  [CANTIDAD_COMPRADA] decimal(18,0),
  [MAX_STOCK_ANUAL_SUCURSAL] decimal(18,0),
  [GANANCIAS_MES_SUC] decimal(18,0),
);

GO

/* FUNCIONES */


CREATE FUNCTION [BREAKFAST_CLUB].OBTENER_RANGO_EDAD(@fecha DATETIME2(3))
RETURNS NVARCHAR(255) AS
BEGIN

	DECLARE @EDAD decimal(18,0) = year(getdate()) - year(@fecha)
	DECLARE @RETORNO NVARCHAR(255) = ' '
	
	SET @RETORNO = CASE
	WHEN (@EDAD >= 18 and @EDAD <= 30) THEN '18-30anios'
	WHEN @EDAD >= 31 and @EDAD <= 50 THEN '31-50anios'
	ELSE '>50anios'
	END

	RETURN @RETORNO

END
GO

CREATE FUNCTION [BREAKFAST_CLUB].OBTENER_CODIGO_TIEMPO(@fecha DATETIME2(3))
RETURNS decimal(18,0) AS
BEGIN

	DECLARE @RETORNO decimal(18,0) = 0
	SET @RETORNO = (SELECT COD_TIEMPO from [BREAKFAST_CLUB].BI_TIEMPO WHERE ANIO = YEAR((@fecha)) AND MES = MONTH((@fecha)))

	RETURN @RETORNO

END
GO

CREATE FUNCTION [BREAKFAST_CLUB].OBTENER_STOCK_DISPONIBLE_SUCURSAL_ACCESORIO(@TIEMPO DECIMAL(18,0), @SUCURSAL DECIMAL(18,0))
RETURNS DECIMAL (18,0)
AS
BEGIN
	DECLARE @RETURN DECIMAL(18,0);
	DECLARE @COMPRAS_ANTERIORES DECIMAL(18,0);
	DECLARE @VENTAS_ANTERIORES DECIMAL(18,0);
		
	SET @VENTAS_ANTERIORES = (SELECT SUM((ISNULL(CANTIDAD_VENDIDA,0))) FROM [BREAKFAST_CLUB].BI_HECHOS_VENTAS_ACCESORIOS WHERE (COD_TIEMPO BETWEEN 1 AND (@TIEMPO-1)) AND (COD_SUCURSAL = @SUCURSAL));

	SET @COMPRAS_ANTERIORES = (SELECT SUM((ISNULL(CANTIDAD_COMPRADA,0))) FROM [BREAKFAST_CLUB].BI_HECHOS_COMPRAS_ACCESORIOS	WHERE (COD_TIEMPO BETWEEN 1 AND @TIEMPO) AND COD_SUCURSAL = @SUCURSAL);

	SET @RETURN = ISNULL(@COMPRAS_ANTERIORES, 0) - ISNULL(@VENTAS_ANTERIORES, 0) ;

	RETURN @RETURN
END
GO

CREATE FUNCTION [BREAKFAST_CLUB].STOCK_MAX_EN_ANIO(@SUCURSAL DECIMAL(18,0), @COD_TIEMPO DECIMAL(18,0))
RETURNS DECIMAL(18,0)
AS 
BEGIN
    DECLARE @RET DECIMAL(18,0)

	DECLARE @ANIO DECIMAL(18,0) = (SELECT ANIO FROM [BREAKFAST_CLUB].BI_TIEMPO WHERE COD_TIEMPO = @COD_TIEMPO)

	DECLARE @PRIMER_MES DECIMAL (18,0) = (SELECT TOP 1 COD_TIEMPO FROM [BREAKFAST_CLUB].BI_TIEMPO 
										WHERE ANIO = @ANIO ORDER BY MES ASC)
	DECLARE @ULTIMO_MES DECIMAL (18,0) = (SELECT TOP 1 COD_TIEMPO FROM [BREAKFAST_CLUB].BI_TIEMPO WHERE ANIO = @ANIO ORDER BY MES DESC)
	
	SET @RET = (SELECT MAX([BREAKFAST_CLUB].OBTENER_STOCK_DISPONIBLE_SUCURSAL_ACCESORIO(COD_TIEMPO, @SUCURSAL))
	FROM [BREAKFAST_CLUB].BI_TIEMPO WHERE COD_TIEMPO BETWEEN @PRIMER_MES AND @ULTIMO_MES) 

    RETURN @RET
END
GO

/* TIEMPO EN STOCK ACCESORIO */

CREATE FUNCTION [BREAKFAST_CLUB].DIFERENCIA_TIEMPO_ACCES_SUCURSAL(@COD_ACCESORIO DECIMAL(18,0), @COD_SUCURSAL DECIMAL(18,0))
RETURNS  DECIMAL(18,0) AS
BEGIN

	DECLARE @ventas TABLE (cant_ventas DECIMAL(18,0), tiempo DECIMAL(18,0))
	INSERT INTO @ventas	SELECT SUM(CANTIDAD_VENDIDA), SUM(CANTIDAD_VENDIDA * COD_TIEMPO) FROM [BREAKFAST_CLUB].BI_HECHOS_VENTAS_ACCESORIOS  
	WHERE COD_ACCESORIO = @COD_ACCESORIO AND COD_SUCURSAL = @COD_SUCURSAL

	DECLARE @CANT_TOTAL_VENTAS DECIMAL(18,0) = (select cant_ventas from @ventas)
	DECLARE @TIEMPO_VENTA DECIMAL(18,0) = (select tiempo from @ventas)

	DECLARE @MES_ANTERIOR DECIMAL(18,0) = (SELECT MAX(A.COD_TIEMPO) FROM (SELECT ha.cod_tiempo AS COD_TIEMPO FROM  [BREAKFAST_CLUB].BI_HECHOS_COMPRAS_ACCESORIOS ha
	JOIN   [BREAKFAST_CLUB].BI_HECHOS_COMPRAS_ACCESORIOS ha1 ON (ha1.COD_TIEMPO <= ha.cod_tiempo 
	and ha.COD_ACCESORIO = ha1.COD_ACCESORIO and ha.COD_SUCURSAL = ha1.COD_SUCURSAL)
	WHERE  ha.CANTIDAD_COMPRADA < @CANT_TOTAL_VENTAS and ha1.COD_ACCESORIO = @COD_ACCESORIO and ha1.COD_SUCURSAL = @COD_SUCURSAL	
	GROUP  BY ha.cod_tiempo	HAVING SUM(ha1.CANTIDAD_COMPRADA) <  @CANT_TOTAL_VENTAS) A)

	DECLARE @compras_anteriores TABLE (cant_compras DECIMAL(18,0), tiempo_compras DECIMAL(18,0))
	INSERT INTO @compras_anteriores SELECT SUM(CANTIDAD_COMPRADA), SUM(CANTIDAD_COMPRADA * COD_TIEMPO) AS TOTAL FROM [BREAKFAST_CLUB].BI_HECHOS_COMPRAS_ACCESORIOS 
	WHERE COD_ACCESORIO = @COD_ACCESORIO AND COD_SUCURSAL = @COD_SUCURSAL AND COD_TIEMPO <= @MES_ANTERIOR

	DECLARE @TIEMPO_COMPRA DECIMAL(18,0) = (select tiempo_compras from @compras_anteriores)
	DECLARE @CANT_TOTAL_MES_ANTERIOR DECIMAL(18,0) = (select cant_compras from @compras_anteriores) 

	SET @TIEMPO_COMPRA = @TIEMPO_COMPRA + (@CANT_TOTAL_VENTAS - @CANT_TOTAL_MES_ANTERIOR) * (@MES_ANTERIOR + 1)
	 
	DECLARE @RETORNO DECIMAL(18,0) = @TIEMPO_VENTA - @TIEMPO_COMPRA

	RETURN @RETORNO
END
GO

CREATE FUNCTION [BREAKFAST_CLUB].TIEMPO_PROMEDIO_EN_STOCK_ACCESORIO(@COD_ACCESORIO DECIMAL(18,0))
RETURNS  DECIMAL(18,4) AS
BEGIN
	DECLARE @RETORNO DECIMAL(18,4) = (SELECT SUM([BREAKFAST_CLUB].DIFERENCIA_TIEMPO_ACCES_SUCURSAL(@COD_ACCESORIO , COD_SUCURSAL))	
									FROM [BREAKFAST_CLUB].BI_SUCURSAL)

	DECLARE @CANT_TOTAL_VENTAS DECIMAL(18,4) = (SELECT SUM(CANTIDAD_VENDIDA) FROM [BREAKFAST_CLUB].BI_HECHOS_VENTAS_ACCESORIOS  
	WHERE COD_ACCESORIO = @COD_ACCESORIO)

	SET @RETORNO = @RETORNO/@CANT_TOTAL_VENTAS

	RETURN @RETORNO
END						
GO

/* TIEMPO EN STOCK PC */

CREATE FUNCTION [BREAKFAST_CLUB].DIFERENCIA_TIEMPO_PC_SUCURSAL(@COD_PC nvarchar(50), @COD_SUCURSAL DECIMAL(18,0))
RETURNS  DECIMAL(18,0) AS
BEGIN

	DECLARE @ventas TABLE (cant_ventas DECIMAL(18,0), tiempo DECIMAL(18,0))
	INSERT INTO @ventas	SELECT SUM(CANTIDAD_VENDIDA), SUM(CANTIDAD_VENDIDA * COD_TIEMPO) FROM [BREAKFAST_CLUB].BI_HECHOS_VENTAS_PC  
	WHERE COD_PC = @COD_PC AND COD_SUCURSAL = @COD_SUCURSAL

	DECLARE @CANT_TOTAL_VENTAS DECIMAL(18,0) = (select cant_ventas from @ventas)
	DECLARE @TIEMPO_VENTA DECIMAL(18,0) = (select tiempo from @ventas)

	DECLARE @MES_ANTERIOR DECIMAL(18,0) = (SELECT MAX(A.COD_TIEMPO) FROM (SELECT ha.cod_tiempo AS COD_TIEMPO FROM   [BREAKFAST_CLUB].BI_HECHOS_COMPRAS_PC ha
	JOIN   [BREAKFAST_CLUB].BI_HECHOS_COMPRAS_PC ha1 ON (ha1.COD_TIEMPO <= ha.cod_tiempo 
	and ha.COD_PC = ha1.COD_PC and ha.COD_SUCURSAL = ha1.COD_SUCURSAL)
	WHERE  ha.CANTIDAD_COMPRADA < @CANT_TOTAL_VENTAS and ha1.COD_PC = @COD_PC and ha1.COD_SUCURSAL = @COD_SUCURSAL
	GROUP  BY ha.cod_tiempo	HAVING SUM(ha1.CANTIDAD_COMPRADA) <  @CANT_TOTAL_VENTAS) A)

	DECLARE @compras_anteriores TABLE (cant_compras DECIMAL(18,0), tiempo_compras DECIMAL(18,0))
	INSERT INTO @compras_anteriores SELECT SUM(CANTIDAD_COMPRADA), SUM(CANTIDAD_COMPRADA * COD_TIEMPO) AS TOTAL FROM [BREAKFAST_CLUB].BI_HECHOS_COMPRAS_PC 
	WHERE COD_PC = @COD_PC AND COD_SUCURSAL = @COD_SUCURSAL AND COD_TIEMPO <= @MES_ANTERIOR

	DECLARE @TIEMPO_COMPRA DECIMAL(18,0) = (select tiempo_compras from @compras_anteriores)
	DECLARE @CANT_TOTAL_MES_ANTERIOR DECIMAL(18,0) = (select cant_compras from @compras_anteriores) 

	SET @TIEMPO_COMPRA = @TIEMPO_COMPRA + (@CANT_TOTAL_VENTAS - @CANT_TOTAL_MES_ANTERIOR) * (@MES_ANTERIOR + 1)
	 
	DECLARE @RETORNO DECIMAL(18,0) = @TIEMPO_VENTA - @TIEMPO_COMPRA

	RETURN @RETORNO

END
GO

CREATE FUNCTION [BREAKFAST_CLUB].TIEMPO_PROMEDIO_EN_STOCK_PC(@COD_PC nvarchar(50))
RETURNS  DECIMAL(18,4) AS
BEGIN
	DECLARE @RETORNO DECIMAL(18,4) = (SELECT SUM([BREAKFAST_CLUB].DIFERENCIA_TIEMPO_PC_SUCURSAL(@COD_PC , COD_SUCURSAL))	
									FROM [BREAKFAST_CLUB].BI_SUCURSAL)

	DECLARE @CANT_TOTAL_VENTAS DECIMAL(18,4) = (SELECT SUM(CANTIDAD_VENDIDA) FROM [BREAKFAST_CLUB].BI_HECHOS_VENTAS_PC  
	WHERE COD_PC = @COD_PC)

	SET @RETORNO = @RETORNO/@CANT_TOTAL_VENTAS

	RETURN @RETORNO
END						
GO

CREATE FUNCTION [BREAKFAST_CLUB].PRECIO_PROMEDIO_COMPRA_PC(@COD_PC nvarchar(50))
RETURNS DECIMAL(18,2) AS
BEGIN
	DECLARE @RET DECIMAL(18,2) = (SELECT ROUND(CAST(AVG(PRECIO) AS decimal(18,1)),1) 
	FROM [BREAKFAST_CLUB].BI_HECHOS_COMPRAS_PC WHERE COD_PC = @COD_PC) 
	RETURN @RET
END
GO

CREATE FUNCTION [BREAKFAST_CLUB].PRECIO_PROMEDIO_VENTAS_PC(@COD_PC nvarchar(50))
RETURNS DECIMAL (18,2) AS
BEGIN
	DECLARE @RET DECIMAL(18,2) = (SELECT ROUND(CAST(AVG(PRECIO) AS decimal(18,1)),1) 
	FROM [BREAKFAST_CLUB].BI_HECHOS_VENTAS_PC WHERE COD_PC = @COD_PC) 
	RETURN @RET
END
GO

CREATE FUNCTION [BREAKFAST_CLUB].PRECIO_PROMEDIO_COMPRA_ACC(@COD_ACC DECIMAL(18,0))
RETURNS DECIMAL(18,2) AS
BEGIN
	DECLARE @RET DECIMAL(18,2) = (SELECT ROUND(CAST(AVG(PRECIO) AS decimal(18,1)),1) 
	FROM [BREAKFAST_CLUB].BI_HECHOS_COMPRAS_ACCESORIOS WHERE COD_ACCESORIO = @COD_ACC GROUP BY COD_ACCESORIO) 
	RETURN @RET
END
GO

CREATE FUNCTION [BREAKFAST_CLUB].PRECIO_PROMEDIO_VENTA_ACC(@COD_ACC DECIMAL(18,0))
RETURNS DECIMAL(18,2) AS
BEGIN
	DECLARE @RET DECIMAL(18,2) = (SELECT ROUND(CAST(AVG(PRECIO) AS decimal(18,1)),1) 
	FROM [BREAKFAST_CLUB].BI_HECHOS_VENTAS_ACCESORIOS WHERE COD_ACCESORIO = @COD_ACC GROUP BY COD_ACCESORIO) 
	RETURN @RET
END
GO


CREATE FUNCTION [BREAKFAST_CLUB].GANANCIAS_ACCESORIOS(@COD_TIEMPO DECIMAL(18,0), @COD_SUCURSAL DECIMAL(18,0))
RETURNS DECIMAL(18,0) AS
BEGIN
	DECLARE @COMPRAS_MES DECIMAL(18,0) = (SELECT SUM(PRECIO * CANTIDAD_COMPRADA) FROM [BREAKFAST_CLUB].BI_HECHOS_COMPRAS_ACCESORIOS 
														WHERE COD_TIEMPO = @COD_TIEMPO AND COD_SUCURSAL = @COD_SUCURSAL)
	DECLARE @VENTAS_MES DECIMAL(18,0) = (SELECT SUM(PRECIO * CANTIDAD_VENDIDA) FROM [BREAKFAST_CLUB].BI_HECHOS_VENTAS_ACCESORIOS 
											WHERE COD_TIEMPO = @COD_TIEMPO AND COD_SUCURSAL = @COD_SUCURSAL)
	RETURN ISNULL(@VENTAS_MES,0) - ISNULL(@COMPRAS_MES,0)
END
GO

CREATE FUNCTION [BREAKFAST_CLUB].GANANCIAS_PC(@COD_TIEMPO DECIMAL(18,0), @COD_SUCURSAL DECIMAL(18,0))
RETURNS DECIMAL(18,0) AS
BEGIN
	DECLARE @COMPRAS_MES DECIMAL(18,0) = (SELECT SUM(PRECIO * CANTIDAD_COMPRADA) FROM [BREAKFAST_CLUB].BI_HECHOS_COMPRAS_PC 
											WHERE COD_TIEMPO = @COD_TIEMPO AND COD_SUCURSAL = @COD_SUCURSAL)
	DECLARE @VENTAS_MES DECIMAL(18,0) = (SELECT SUM(PRECIO * CANTIDAD_VENDIDA) FROM [BREAKFAST_CLUB].BI_HECHOS_VENTAS_PC 
											WHERE COD_TIEMPO = @COD_TIEMPO AND COD_SUCURSAL = @COD_SUCURSAL)
	RETURN ISNULL(@VENTAS_MES,0) - ISNULL(@COMPRAS_MES,0)
END
GO

CREATE FUNCTION [BREAKFAST_CLUB].TOTAL_VENDIDO_X_MES_SUC_ACC(@COD_TIEMPO DECIMAL(18,0), @COD_SUCURSAL DECIMAL(18,0) , @COD_ACCESORIO DECIMAL(18,0))
RETURNS DECIMAL(18,0) AS
BEGIN
	RETURN (SELECT SUM(CANTIDAD_VENDIDA) FROM [BREAKFAST_CLUB].BI_HECHOS_VENTAS_ACCESORIOS 
			WHERE COD_TIEMPO = @COD_TIEMPO AND COD_SUCURSAL = @COD_SUCURSAL AND COD_ACCESORIO = @COD_ACCESORIO)
END
GO

CREATE FUNCTION [BREAKFAST_CLUB].TOTAL_VENDIDO_X_MES_SUC_PC(@COD_TIEMPO DECIMAL(18,0), @COD_SUCURSAL DECIMAL(18,0) , @COD_PC NVARCHAR(50))
RETURNS DECIMAL(18,0) AS
BEGIN
	RETURN (SELECT SUM(CANTIDAD_VENDIDA) FROM [BREAKFAST_CLUB].BI_HECHOS_VENTAS_PC
			WHERE COD_TIEMPO = @COD_TIEMPO AND COD_SUCURSAL = @COD_SUCURSAL AND COD_PC = @COD_PC)
END
GO

/* PROCEDURES */

/* TIEMPO */

CREATE PROCEDURE [BREAKFAST_CLUB].PRC_INSERT_BI_TIEMPO
AS
BEGIN TRANSACTION

INSERT INTO [BREAKFAST_CLUB].[BI_Tiempo] 
SELECT DISTINCT *
FROM 
(SELECT YEAR (F.FECHA) AS ANIO  , MONTH(F.FECHA) AS MES
FROM [BREAKFAST_CLUB].FACTURA F
UNION 
SELECT YEAR (C.FECHA) AS ANIO  , MONTH(C.FECHA) AS MES 
FROM [BREAKFAST_CLUB].COMPRA C) A ORDER BY ANIO, MES ASC

COMMIT;
GO

/* SUCURSAL */

CREATE PROCEDURE [BREAKFAST_CLUB].PRC_INSERT_BI_SUCURSAL
AS
BEGIN TRANSACTION
INSERT INTO [BREAKFAST_CLUB].[BI_SUCURSAL]
SELECT * FROM [BREAKFAST_CLUB].[SUCURSAL]

COMMIT;
GO


/* FABRICANTE */

CREATE PROCEDURE [BREAKFAST_CLUB].PRC_INSERT_BI_FABRICANTE
AS
BEGIN TRANSACTION
INSERT INTO [BREAKFAST_CLUB].[BI_FABRICANTE]
SELECT * FROM [BREAKFAST_CLUB].[FABRICANTE]

COMMIT;
GO
	
/* GPU */

CREATE PROCEDURE [BREAKFAST_CLUB].PRC_INSERT_BI_GPU
AS
BEGIN TRANSACTION
INSERT INTO [BREAKFAST_CLUB].[BI_GPU]
SELECT * FROM [BREAKFAST_CLUB].[GPU]

COMMIT;
GO
   
/* CPU */

CREATE PROCEDURE [BREAKFAST_CLUB].PRC_INSERT_BI_CPU
AS
BEGIN TRANSACTION
INSERT INTO [BREAKFAST_CLUB].[BI_CPU]
SELECT * FROM [BREAKFAST_CLUB].[CPU]

COMMIT;
GO
	
/* DISCO RIGIDO */

CREATE PROCEDURE [BREAKFAST_CLUB].PRC_INSERT_BI_DISCO_RIGIDO
AS
BEGIN TRANSACTION
INSERT INTO [BREAKFAST_CLUB].[BI_DISCO_RIGIDO]
SELECT * FROM [BREAKFAST_CLUB].[DISCO_RIGIDO]

COMMIT;
GO

/* RAM */
    
CREATE PROCEDURE [BREAKFAST_CLUB].PRC_INSERT_BI_RAM
AS
BEGIN TRANSACTION
INSERT INTO [BREAKFAST_CLUB].[BI_RAM]
SELECT * FROM [BREAKFAST_CLUB].[RAM]

COMMIT;
GO
    


/* HECHOS ACCESORIOS */

CREATE PROCEDURE [BREAKFAST_CLUB].PRC_INSERT_BI_HECHOS_ACCESORIOS
AS
BEGIN TRANSACTION
	/*INSERTAR LOS ACCESORIOS */
	BEGIN TRANSACTION
	INSERT INTO [BREAKFAST_CLUB].[BI_ACCESORIO] ([COD_ACCES] , [DESCRIPCION])
	SELECT COD_ACCES, DESCRIPCION FROM [BREAKFAST_CLUB].[ACCESORIO]
	COMMIT;

	/*COMPRAS ACCESORIOS POR SUCURSAL POR MES */
	BEGIN TRANSACTION
	INSERT INTO [BREAKFAST_CLUB].[BI_HECHOS_COMPRAS_ACCESORIOS] ([COD_ACCESORIO], [COD_SUCURSAL], [COD_TIEMPO], [PRECIO],
	[CANTIDAD_COMPRADA])
	SELECT AxC.COD_ACCES, S.COD_SUCURSAL, COD_TIEMPO, AxC.PRECIO, SUM(CANTIDAD)
	FROM [BREAKFAST_CLUB].ACCESORIO_X_COMPRA AxC 
	INNER JOIN [BREAKFAST_CLUB].COMPRA C ON AxC.NRO_COMPRA = C.NRO_COMPRA
	INNER JOIN [BREAKFAST_CLUB].SUCURSAL S ON S.COD_SUCURSAL = C.COD_SUCURSAL
	INNER JOIN [BREAKFAST_CLUB].BI_TIEMPO ON [BREAKFAST_CLUB].OBTENER_CODIGO_TIEMPO(C.FECHA) = COD_TIEMPO
	GROUP BY COD_TIEMPO, AxC.COD_ACCES, S.COD_SUCURSAL, AxC.PRECIO
	ORDER BY COD_TIEMPO
	COMMIT;

	/*VENTAS ACCESORIOS POR SUCURSAL POR MES */
	BEGIN TRANSACTION
	INSERT INTO [BREAKFAST_CLUB].[BI_HECHOS_VENTAS_ACCESORIOS] ([COD_ACCESORIO], [COD_SUCURSAL], [COD_TIEMPO], [PRECIO], [CANTIDAD_VENDIDA], [EDAD])
	SELECT AxF.COD_ACCES, S.COD_SUCURSAL, COD_TIEMPO, AxF.PRECIO, SUM(CANTIDAD), [BREAKFAST_CLUB].OBTENER_RANGO_EDAD(FECHA_NAC)
	FROM [BREAKFAST_CLUB].ACCESORIO_X_FACTURA AxF
	INNER JOIN [BREAKFAST_CLUB].FACTURA F ON AxF.NRO_FACTURA = F.NRO_FACTURA
	INNER JOIN [BREAKFAST_CLUB].SUCURSAL S ON S.COD_SUCURSAL = F.COD_SUCURSAL
	INNER JOIN [BREAKFAST_CLUB].BI_TIEMPO ON COD_TIEMPO = [BREAKFAST_CLUB].OBTENER_CODIGO_TIEMPO(FECHA)
	INNER JOIN [BREAKFAST_CLUB].CLIENTE CLI ON CLI.COD_CLIENTE = F.COD_CLIENTE
	GROUP BY S.COD_SUCURSAL, AxF.COD_ACCES, AxF.PRECIO, COD_TIEMPO, [BREAKFAST_CLUB].OBTENER_RANGO_EDAD(FECHA_NAC)
	ORDER BY COD_TIEMPO
	COMMIT;


	/* UPDATE HECHOS VENTAS ACC */
	CREATE TABLE [BREAKFAST_CLUB].#GANANCIAS_TOTAL_VENDIDO_STOCK_ACC
	(COD_TIEMPO DECIMAL(18,0), COD_SUCURSAL DECIMAL(18,0), COD_ACCESORIO DECIMAL(18,0), MAX_STOCK DECIMAL(18,0), GANANCIAS DECIMAL(18,0), TOTAL_VENDIDO DECIMAL(18,0))
	
	INSERT INTO [BREAKFAST_CLUB].#GANANCIAS_TOTAL_VENDIDO_STOCK_ACC (COD_TIEMPO, COD_SUCURSAL, COD_ACCESORIO, MAX_STOCK , GANANCIAS, TOTAL_VENDIDO) 
	SELECT COD_TIEMPO, COD_SUCURSAL, COD_ACCES,
	[BREAKFAST_CLUB].STOCK_MAX_EN_ANIO(COD_SUCURSAL, COD_TIEMPO),
	[BREAKFAST_CLUB].GANANCIAS_ACCESORIOS(COD_TIEMPO, COD_SUCURSAL),
	[BREAKFAST_CLUB].TOTAL_VENDIDO_X_MES_SUC_ACC(COD_TIEMPO, COD_SUCURSAL, COD_ACCES)
	FROM (SELECT DISTINCT COD_TIEMPO, COD_SUCURSAL, COD_ACCES 
	FROM [BREAKFAST_CLUB].BI_TIEMPO, [BREAKFAST_CLUB].BI_ACCESORIO, [BREAKFAST_CLUB].BI_SUCURSAL) TSA

	BEGIN TRANSACTION
	UPDATE H SET H.[TOTAL_VENDIDO_MES_SUC_ACC] = (SELECT TOTAL_VENDIDO FROM [BREAKFAST_CLUB].#GANANCIAS_TOTAL_VENDIDO_STOCK_ACC AS TV 
												WHERE H.COD_TIEMPO = TV.COD_TIEMPO AND H.COD_SUCURSAL = TV.COD_SUCURSAL 
												AND H.COD_ACCESORIO = TV.COD_ACCESORIO),
				H.[GANANCIAS_MES_SUC] = (SELECT GANANCIAS FROM [BREAKFAST_CLUB].#GANANCIAS_TOTAL_VENDIDO_STOCK_ACC AS TV 
												WHERE H.COD_TIEMPO = TV.COD_TIEMPO AND H.COD_SUCURSAL = TV.COD_SUCURSAL 
												AND H.COD_ACCESORIO = TV.COD_ACCESORIO),
				H.[MAX_STOCK_ANUAL_SUCURSAL] = (SELECT MAX_STOCK FROM [BREAKFAST_CLUB].#GANANCIAS_TOTAL_VENDIDO_STOCK_ACC AS TV 
												WHERE H.COD_TIEMPO = TV.COD_TIEMPO AND H.COD_SUCURSAL = TV.COD_SUCURSAL 
												AND H.COD_ACCESORIO = TV.COD_ACCESORIO)
	FROM [BREAKFAST_CLUB].[BI_HECHOS_VENTAS_ACCESORIOS] H
	COMMIT;

	/* UPDATE HECHOS COMPRAS ACC */
	BEGIN TRANSACTION
	UPDATE H SET 
	H.[MAX_STOCK_ANUAL_SUCURSAL] = (SELECT MAX_STOCK FROM [BREAKFAST_CLUB].#GANANCIAS_TOTAL_VENDIDO_STOCK_ACC AS TV 
												WHERE H.COD_TIEMPO = TV.COD_TIEMPO AND H.COD_SUCURSAL = TV.COD_SUCURSAL 
												AND H.COD_ACCESORIO = TV.COD_ACCESORIO),
	H.[GANANCIAS_MES_SUC] = (SELECT GANANCIAS FROM [BREAKFAST_CLUB].#GANANCIAS_TOTAL_VENDIDO_STOCK_ACC AS TV 
												WHERE H.COD_TIEMPO = TV.COD_TIEMPO AND H.COD_SUCURSAL = TV.COD_SUCURSAL 
												AND H.COD_ACCESORIO = TV.COD_ACCESORIO)
	FROM [BREAKFAST_CLUB].BI_HECHOS_COMPRAS_ACCESORIOS H
	COMMIT;

	DROP TABLE [BREAKFAST_CLUB].#GANANCIAS_TOTAL_VENDIDO_STOCK_ACC

	/* UPDATE ACCESORIO */
	BEGIN TRANSACTION
	UPDATE [BREAKFAST_CLUB].[BI_ACCESORIO] SET [TIEMPO_PROMEDIO_EN_STOCK] = [BREAKFAST_CLUB].TIEMPO_PROMEDIO_EN_STOCK_ACCESORIO(COD_ACCES),
	   [PRECIO_PROMEDIO_VENTA] = [BREAKFAST_CLUB].PRECIO_PROMEDIO_VENTA_ACC(COD_ACCES), 
	   [PRECIO_PROMEDIO_COMPRA] = [BREAKFAST_CLUB].PRECIO_PROMEDIO_COMPRA_ACC(COD_ACCES)
	COMMIT;

COMMIT;
GO


/* HECHOS PC */

CREATE PROCEDURE [BREAKFAST_CLUB].PRC_INSERT_BI_HECHOS_PC
AS
BEGIN TRANSACTION

	/* PC */
	BEGIN TRANSACTION
	INSERT INTO [BREAKFAST_CLUB].[BI_PC]  ([COD_PC], [ALTO], [ANCHO] , [PROFUNDIDAD])
	SELECT COD_PC, ALTO, ANCHO, PROFUNDIDAD FROM [BREAKFAST_CLUB].[PC]
	COMMIT;


	/* VENTAS PC POR SUCURSAL POR MES */
	BEGIN TRANSACTION
	INSERT INTO [BREAKFAST_CLUB].[BI_HECHOS_COMPRAS_PC] ([COD_PC], [COD_RAM], [COD_DISCO], [COD_GPU], [COD_CPU], [COD_SUCURSAL], [COD_TIEMPO],  [PRECIO],[CANTIDAD_COMPRADA])
	SELECT PxC.COD_PC, COD_RAM, COD_DISCO, COD_GPU, COD_CPU, S.COD_SUCURSAL, COD_TIEMPO, PxC.PRECIO, SUM(CANTIDAD)
	FROM [BREAKFAST_CLUB].PC_X_COMPRA PxC 
	INNER JOIN [BREAKFAST_CLUB].COMPRA C ON PxC.NRO_COMPRA = C.NRO_COMPRA
	INNER JOIN [BREAKFAST_CLUB].SUCURSAL S ON S.COD_SUCURSAL = C.COD_SUCURSAL
	INNER JOIN [BREAKFAST_CLUB].PC PC ON PC.COD_PC = PxC.COD_PC
	INNER JOIN [BREAKFAST_CLUB].BI_TIEMPO ON COD_TIEMPO = [BREAKFAST_CLUB].OBTENER_CODIGO_TIEMPO(FECHA)
	GROUP BY COD_TIEMPO, PxC.COD_PC, COD_GPU,  COD_RAM, COD_DISCO, COD_CPU, S.COD_SUCURSAL, PxC.PRECIO
	ORDER BY COD_TIEMPO
	COMMIT;

	/*COMPRAS ACCESORIOS POR SUCURSAL POR MES */
	BEGIN TRANSACTION 
	INSERT INTO [BREAKFAST_CLUB].[BI_HECHOS_VENTAS_PC] ([COD_PC], [COD_RAM], [COD_DISCO], [COD_GPU], [COD_CPU], [COD_SUCURSAL], [EDAD],
    [COD_TIEMPO], [PRECIO], [CANTIDAD_VENDIDA])
	SELECT PxF.COD_PC, COD_RAM, COD_DISCO, COD_GPU, COD_CPU, S.COD_SUCURSAL, [BREAKFAST_CLUB].OBTENER_RANGO_EDAD(FECHA_NAC), COD_TIEMPO, PxF.PRECIO, SUM(CANTIDAD)
	FROM [BREAKFAST_CLUB].PC_X_FACTURA PxF 
	INNER JOIN [BREAKFAST_CLUB].FACTURA F ON PxF.NRO_FACTURA = F.NRO_FACTURA
	INNER JOIN [BREAKFAST_CLUB].SUCURSAL S ON S.COD_SUCURSAL = F.COD_SUCURSAL
	INNER JOIN [BREAKFAST_CLUB].PC PC ON PC.COD_PC = PxF.COD_PC
	INNER JOIN [BREAKFAST_CLUB].BI_TIEMPO ON [BREAKFAST_CLUB].OBTENER_CODIGO_TIEMPO(FECHA) = COD_TIEMPO
	INNER JOIN [BREAKFAST_CLUB].CLIENTE CLI ON CLI.COD_CLIENTE = F.COD_CLIENTE
	GROUP BY COD_TIEMPO, PxF.COD_PC, COD_GPU,  COD_RAM, COD_DISCO, COD_CPU, S.COD_SUCURSAL, PxF.PRECIO, [BREAKFAST_CLUB].OBTENER_RANGO_EDAD(FECHA_NAC)
	ORDER BY COD_TIEMPO
	COMMIT;


	/* UPDATE HECHOS VENTAS PC */
	CREATE TABLE [BREAKFAST_CLUB].#GANANCIAS_TOTAL_VENDIDO_PC (COD_TIEMPO DECIMAL(18,0), COD_SUCURSAL DECIMAL(18,0), COD_PC NVARCHAR(50), GANANCIAS DECIMAL(18,0), TOTAL_VENDIDO DECIMAL(18,0))
	
	INSERT INTO [BREAKFAST_CLUB].#GANANCIAS_TOTAL_VENDIDO_PC (COD_TIEMPO, COD_SUCURSAL, COD_PC, GANANCIAS, TOTAL_VENDIDO) 
	SELECT COD_TIEMPO, COD_SUCURSAL, COD_PC, [BREAKFAST_CLUB].GANANCIAS_PC(COD_TIEMPO, COD_SUCURSAL) GANANCIAS,
	[BREAKFAST_CLUB].TOTAL_VENDIDO_X_MES_SUC_PC(COD_TIEMPO, COD_SUCURSAL, COD_PC) TOTAL_VENDIDO 
	FROM (SELECT DISTINCT COD_TIEMPO, COD_SUCURSAL, COD_PC FROM [BREAKFAST_CLUB].BI_TIEMPO, [BREAKFAST_CLUB].BI_SUCURSAL, [BREAKFAST_CLUB].BI_PC ) V

	BEGIN TRANSACTION
	UPDATE H SET H.[TOTAL_VENDIDO_MES_SUC_PC] = (SELECT TOTAL_VENDIDO FROM [BREAKFAST_CLUB].#GANANCIAS_TOTAL_VENDIDO_PC AS TV 
												WHERE H.COD_TIEMPO = TV.COD_TIEMPO AND H.COD_SUCURSAL = TV.COD_SUCURSAL 
												AND H.COD_PC = TV.COD_PC),
				H.[GANANCIAS_MES_SUC] = (SELECT GANANCIAS FROM [BREAKFAST_CLUB].#GANANCIAS_TOTAL_VENDIDO_PC AS TV 
												WHERE H.COD_TIEMPO = TV.COD_TIEMPO AND H.COD_SUCURSAL = TV.COD_SUCURSAL 
												AND H.COD_PC = TV.COD_PC)
	FROM [BREAKFAST_CLUB].[BI_HECHOS_VENTAS_PC] H
	COMMIT;

	/* UPDATE HECHOS COMPRAS PC */
	BEGIN TRANSACTION
	UPDATE H SET H.[GANANCIAS_MES_SUC] = (SELECT GANANCIAS FROM [BREAKFAST_CLUB].#GANANCIAS_TOTAL_VENDIDO_PC AS TV 
												WHERE H.COD_TIEMPO = TV.COD_TIEMPO AND H.COD_SUCURSAL = TV.COD_SUCURSAL 
												AND H.COD_PC = TV.COD_PC)
	FROM [BREAKFAST_CLUB].[BI_HECHOS_COMPRAS_PC] H
	COMMIT;

	DROP TABLE [BREAKFAST_CLUB].#GANANCIAS_TOTAL_VENDIDO_PC

	/* UPDATE PC */

	BEGIN TRANSACTION
	UPDATE [BREAKFAST_CLUB].[BI_PC] SET [TIEMPO_PROMEDIO_EN_STOCK] = [BREAKFAST_CLUB].TIEMPO_PROMEDIO_EN_STOCK_PC(COD_PC),
	[PRECIO_PROMEDIO_VENTA] = [BREAKFAST_CLUB].PRECIO_PROMEDIO_VENTAS_PC(COD_PC),
	[PRECIO_PROMEDIO_COMPRA] = [BREAKFAST_CLUB].PRECIO_PROMEDIO_COMPRA_PC(COD_PC)

	COMMIT;

COMMIT;
GO



/* VISTAS */

/* VISTAS PC */
/* Promedio de tiempo en stock de cada modelo de Pc */
CREATE VIEW [BREAKFAST_CLUB].BI_PROMEDIO_TIEMPO_EN_STOCK_PC AS
SELECT COD_PC, TIEMPO_PROMEDIO_EN_STOCK
FROM [BREAKFAST_CLUB].BI_PC
GROUP BY COD_PC, TIEMPO_PROMEDIO_EN_STOCK
GO


/* Precio promedio de PCs, vendidos y comprados */
CREATE VIEW [BREAKFAST_CLUB].BI_PRECIO_PROMEDIO_PC AS
SELECT COD_PC, PRECIO_PROMEDIO_COMPRA, PRECIO_PROMEDIO_VENTA
FROM [BREAKFAST_CLUB].BI_PC GROUP BY COD_PC, PRECIO_PROMEDIO_COMPRA, PRECIO_PROMEDIO_VENTA
GO

/* Cantidad de PCs, vendidos y comprados x sucursal y mes */
CREATE VIEW [BREAKFAST_CLUB].BI_CANT_COMPRAS_VENTAS_PC_X_SUCURSAL_X_MES AS
SELECT TSC.COD_SUCURSAL, MES, ANIO , TSC.COD_PC, ISNULL(CANTIDAD_COMPRADA,0) AS CANTIDAD_COMPRADA, ISNULL(TOTAL_VENDIDO_MES_SUC_PC,0) AS CANTIDAD_VENDIDA
FROM (SELECT COD_TIEMPO, ANIO, MES, COD_SUCURSAL,COD_PC FROM [BREAKFAST_CLUB].BI_TIEMPO , [BREAKFAST_CLUB].BI_SUCURSAL, [BREAKFAST_CLUB].BI_PC) TSC 
LEFT JOIN (SELECT CANTIDAD_COMPRADA, COD_TIEMPO, COD_SUCURSAL, COD_PC FROM [BREAKFAST_CLUB].BI_HECHOS_COMPRAS_PC) C 
ON C.COD_TIEMPO = TSC.COD_TIEMPO AND C.COD_SUCURSAL = TSC.COD_SUCURSAL AND C.COD_PC = TSC.COD_PC 
LEFT JOIN (SELECT DISTINCT COD_SUCURSAL, COD_TIEMPO, TOTAL_VENDIDO_MES_SUC_PC, COD_PC FROM [BREAKFAST_CLUB].BI_HECHOS_VENTAS_PC) V 
ON TSC.COD_PC = V.COD_PC AND TSC.COD_SUCURSAL = V.COD_SUCURSAL AND TSC.COD_TIEMPO = V.COD_TIEMPO
GO


-- Ganancias (precio de venta – precio de compra) x Sucursal x mes 
CREATE VIEW [BREAKFAST_CLUB].BI_GANANCIAS_PC_X_SUCURSAL_X_MES AS
SELECT DISTINCT TS.COD_SUCURSAL, MES, ANIO, ISNULL(GANANCIAS_MES_SUC,0) AS GANANCIAS
FROM (SELECT * FROM [BREAKFAST_CLUB].BI_TIEMPO, [BREAKFAST_CLUB].BI_SUCURSAL) TS LEFT JOIN
(SELECT DISTINCT COD_TIEMPO, COD_SUCURSAL, GANANCIAS_MES_SUC FROM [BREAKFAST_CLUB].BI_HECHOS_COMPRAS_PC 
UNION SELECT DISTINCT COD_TIEMPO, COD_SUCURSAL, GANANCIAS_MES_SUC FROM [BREAKFAST_CLUB].BI_HECHOS_VENTAS_PC) VC 
ON VC.COD_TIEMPO = TS.COD_TIEMPO AND VC.COD_SUCURSAL = TS.COD_SUCURSAL
GO

-- VISTAS ACCESORIOS 
-- Precio promedio de cada accesorio, vendido y comprado 
CREATE VIEW [BREAKFAST_CLUB].BI_PRECIO_PROMEDIO_ACCESORIO AS
SELECT COD_ACCES, PRECIO_PROMEDIO_COMPRA, PRECIO_PROMEDIO_VENTA
FROM [BREAKFAST_CLUB].BI_ACCESORIO GROUP BY COD_ACCES, PRECIO_PROMEDIO_COMPRA, PRECIO_PROMEDIO_VENTA
GO

-- Ganancias (precio de venta – precio de compra) x Sucursal x mes 
CREATE VIEW [BREAKFAST_CLUB].BI_GANANCIAS_ACCESORIO_X_SUCURSAL_X_MES AS
SELECT DISTINCT TS.COD_SUCURSAL, MES, ANIO, ISNULL(GANANCIAS_MES_SUC,0) AS GANANCIAS
FROM (SELECT * FROM [BREAKFAST_CLUB].BI_TIEMPO, [BREAKFAST_CLUB].BI_SUCURSAL) TS LEFT JOIN
(SELECT DISTINCT COD_TIEMPO, COD_SUCURSAL, GANANCIAS_MES_SUC FROM [BREAKFAST_CLUB].BI_HECHOS_COMPRAS_ACCESORIOS 
UNION SELECT DISTINCT COD_TIEMPO, COD_SUCURSAL, GANANCIAS_MES_SUC FROM [BREAKFAST_CLUB].BI_HECHOS_VENTAS_ACCESORIOS) VC 
ON VC.COD_TIEMPO = TS.COD_TIEMPO AND VC.COD_SUCURSAL = TS.COD_SUCURSAL
GO

-- Promedio de tiempo en stock de cada modelo de accesorio 
CREATE VIEW [BREAKFAST_CLUB].BI_PROMEDIO_TIEMPO_EN_STOCK_ACCESORIO AS
SELECT COD_ACCES, TIEMPO_PROMEDIO_EN_STOCK AS TIEMPO_EN_STOCK_PROMEDIO_EN_MESES
FROM [BREAKFAST_CLUB].BI_ACCESORIO
GO

-- Máxima cantidad de stock por cada sucursal (anual)
CREATE VIEW [BREAKFAST_CLUB].BI_CANT_MAXIMA_EN_STOCK_X_SUCURSAL_X_ANIO AS
SELECT DISTINCT T.COD_SUCURSAL, T.ANIO, T.MAX_STOCK_ANUAL_SUCURSAL
FROM (SELECT DISTINCT COD_SUCURSAL, ANIO, MAX_STOCK_ANUAL_SUCURSAL FROM [BREAKFAST_CLUB].BI_TIEMPO T JOIN [BREAKFAST_CLUB].BI_HECHOS_COMPRAS_ACCESORIOS C
ON T.COD_TIEMPO = C.COD_TIEMPO
UNION
SELECT DISTINCT COD_SUCURSAL, ANIO, MAX_STOCK_ANUAL_SUCURSAL FROM [BREAKFAST_CLUB].BI_TIEMPO T JOIN [BREAKFAST_CLUB].BI_HECHOS_VENTAS_ACCESORIOS V
ON T.COD_TIEMPO = V.COD_TIEMPO) T
GO


EXEC [BREAKFAST_CLUB].PRC_INSERT_BI_TIEMPO
EXEC [BREAKFAST_CLUB].PRC_INSERT_BI_FABRICANTE
EXEC [BREAKFAST_CLUB].PRC_INSERT_BI_SUCURSAL
EXEC [BREAKFAST_CLUB].PRC_INSERT_BI_GPU
EXEC [BREAKFAST_CLUB].PRC_INSERT_BI_CPU
EXEC [BREAKFAST_CLUB].PRC_INSERT_BI_DISCO_RIGIDO
EXEC [BREAKFAST_CLUB].PRC_INSERT_BI_RAM

EXEC [BREAKFAST_CLUB].PRC_INSERT_BI_HECHOS_ACCESORIOS
EXEC [BREAKFAST_CLUB].PRC_INSERT_BI_HECHOS_PC


GO