USE [GD1C2021]
GO

CREATE TABLE [BREAKFAST_CLUB].[BI_TIEMPO] (
  [COD_TIEMPO] decimal(18,0) IDENTITY (1,1),
  [ANIO] int,
  [MES] int,
  PRIMARY KEY ([COD_TIEMPO]),
  CONSTRAINT ck_tiempo_mes CHECK ([MES] BETWEEN 0 AND 12) 
);

CREATE TABLE [BREAKFAST_CLUB].[BI_CLIENTE] (
  [COD_CLIENTE] decimal(18,0) NOT NULL,
  [NOMBRE] nvarchar(255) NOT NULL,
  [APELLIDO] nvarchar(255) NOT NULL,
  [DNI] decimal(18,0) NOT NULL,
  [EDAD] nvarchar(255) NOT NULL,
  [MAIL] nvarchar(255) ,
  [TELEFONO] int ,
  [DIRECCION] nvarchar(255),
  PRIMARY KEY ([COD_CLIENTE]),
  CONSTRAINT ck_cliente_edad CHECK ([EDAD] IN ('18-30anios','31-50anios', '>50anios'))
);

CREATE TABLE [BREAKFAST_CLUB].[BI_SUCURSAL](
	[COD_SUCURSAL] decimal (18,0),
	[COD_CIUDAD] decimal (18,0) REFERENCES [BREAKFAST_CLUB].CIUDAD,
	[MAIL] nvarchar(255),
	[TELEFONO] decimal (18,0),
	[DIRECCION] nvarchar(255),
	CONSTRAINT COD_BI_SUCURSAL_PK PRIMARY KEY ([COD_SUCURSAL])
);

CREATE TABLE [BREAKFAST_CLUB].[BI_PC] (
    [COD_PC] nvarchar(50),
    [ALTO] decimal(18,2),
    [ANCHO] decimal(18,2),
    [PROFUNDIDAD] decimal(18,2),
    CONSTRAINT COD_BI_PC_PK PRIMARY KEY ([COD_PC])
);

CREATE TABLE [BREAKFAST_CLUB].[BI_FABRICANTE](
	[COD_FABRICANTE] decimal (18,0),
	[NOMBRE] nvarchar(255),
	CONSTRAINT FABRICANTE_BI_PK PRIMARY KEY ([COD_FABRICANTE])
);

CREATE TABLE [BREAKFAST_CLUB].[BI_GPU](
    [COD_GPU] decimal (18,0),
    [CAPACIDAD] nvarchar(255),
    [MODELO] nvarchar(255),
	[COD_FABRICANTE] decimal(18,0) REFERENCES [BREAKFAST_CLUB].[BI_FABRICANTE],
    [CHIPSET] nvarchar(50),
    [VELOCIDAD] nvarchar(50),
    CONSTRAINT GPU_BI_PK PRIMARY KEY ([COD_GPU])
);

CREATE TABLE [BREAKFAST_CLUB].[BI_CPU] (
    [COD_CPU] nvarchar(50),
    [CACHE] nvarchar(50),
    [CANT_HILOS] nvarchar(50),
	[COD_FABRICANTE] decimal(18,0) REFERENCES [BREAKFAST_CLUB].[BI_FABRICANTE],
    [VELOCIDAD] varchar(50),
    CONSTRAINT CPU_BI_PK PRIMARY KEY ([COD_CPU])
);

CREATE TABLE [BREAKFAST_CLUB].[BI_DISCO_RIGIDO] (
    [COD_DISCO] nvarchar(255),
    [TIPO] nvarchar(255),
    [CAPACIDAD] nvarchar(255),
	[COD_FABRICANTE] decimal(18,0) REFERENCES [BREAKFAST_CLUB].[BI_FABRICANTE],
    [VELOCIDAD] nvarchar(255),
    CONSTRAINT DISCO_RIGIDO_BI_PK PRIMARY KEY ([COD_DISCO])
);

CREATE TABLE [BREAKFAST_CLUB].[BI_RAM] (
    [COD_RAM] nvarchar(255),
    [TIPO] nvarchar(255),
    [CAPACIDAD] nvarchar(255),
	[COD_FABRICANTE] decimal(18,0) REFERENCES [BREAKFAST_CLUB].[BI_FABRICANTE],
    [VELOCIDAD] nvarchar(255),
    CONSTRAINT COD_RAM_BI_PK PRIMARY KEY ([COD_RAM])
);

CREATE TABLE [BREAKFAST_CLUB].[BI_ACCESORIO] (
   [COD_ACCES] decimal(18,0),
   [DESCRIPCION] nvarchar(255),
   CONSTRAINT cod_accesorio_BI_pk PRIMARY KEY ([COD_ACCES])
);

CREATE TABLE [BREAKFAST_CLUB].[BI_HECHOS_PC] (
  [TIPO_TRANSACCION] nvarchar(50),
  [COD_PC] nvarchar(50) REFERENCES [BREAKFAST_CLUB].[BI_PC],
  [COD_RAM] nvarchar(255) REFERENCES [BREAKFAST_CLUB].[BI_RAM],
  [COD_DISCO] nvarchar(255) REFERENCES [BREAKFAST_CLUB].[BI_DISCO_RIGIDO],
  [COD_GPU] decimal (18,0) REFERENCES [BREAKFAST_CLUB].[BI_GPU],
  [COD_CPU] nvarchar(50) REFERENCES [BREAKFAST_CLUB].[BI_CPU],
  [COD_SUCURSAL] decimal (18,0) REFERENCES [BREAKFAST_CLUB].[BI_SUCURSAL],
  [COD_CLIENTE] decimal(18,0) REFERENCES [BREAKFAST_CLUB].[BI_CLIENTE],
  [COD_TIEMPO] decimal(18,0) REFERENCES [BREAKFAST_CLUB].[BI_TIEMPO],
  [PRECIO] decimal(18,2) ,
  [CANTIDAD] decimal(18,0),
  CONSTRAINT ck_tipo_transaccion_BI_pc CHECK ([TIPO_TRANSACCION] IN ('Compra','Venta'))
);

CREATE TABLE [BREAKFAST_CLUB].[BI_HECHOS_ACCESORIOS] (
  [TIPO_TRANSACCION] nvarchar(50),
  [COD_ACCESORIO] decimal(18,0) REFERENCES [BREAKFAST_CLUB].[BI_ACCESORIO],
  [COD_SUCURSAL] decimal (18,0) REFERENCES [BREAKFAST_CLUB].[BI_SUCURSAL],
  [COD_TIEMPO] decimal(18,0) REFERENCES [BREAKFAST_CLUB].[BI_TIEMPO],
  [PRECIO] decimal(18,2) ,
  [CANTIDAD] decimal(18,0),
  [COD_CLIENTE] decimal(18,0) REFERENCES [BREAKFAST_CLUB].[BI_CLIENTE],
  CONSTRAINT ck_tipo_transaccion_BI_acc CHECK ([TIPO_TRANSACCION] IN ('Compra','Venta'))
);

GO

/* FUNCIONES */


CREATE FUNCTION [BREAKFAST_CLUB].OBTENER_RANGO_EDAD(@fecha DATETIME2(3))
RETURNS NVARCHAR(255) AS
BEGIN

	DECLARE @EDAD decimal(18,0) = year(getdate()) - year(@fecha)
	DECLARE @RETORNO NVARCHAR(255) = ' '
	
	SET @RETORNO = CASE
	WHEN (@EDAD >= 18 and @EDAD <= 30) THEN '18-30anios'
	WHEN @EDAD >= 31 and @EDAD <= 50 THEN '31-50anios'
	ELSE '>50anios'
	END

	RETURN @RETORNO

END
GO

CREATE FUNCTION [BREAKFAST_CLUB].OBTENER_CODIGO_TIEMPO(@fecha DATETIME2(3))
RETURNS decimal(18,0) AS
BEGIN

	DECLARE @RETORNO decimal(18,0) = 0
	SET @RETORNO = (SELECT COD_TIEMPO from [BREAKFAST_CLUB].BI_TIEMPO WHERE ANIO = YEAR((@fecha)) AND MES = MONTH((@fecha)))

	RETURN @RETORNO

END
GO

CREATE FUNCTION [BREAKFAST_CLUB].OBTENER_STOCK_DISPONIBLE_SUCURSAL_ACCESORIO(@TIEMPO DECIMAL(18,0), @SUCURSAL DECIMAL(18,0))
RETURNS DECIMAL (18,0)
AS
BEGIN
	DECLARE @RETURN DECIMAL(18,0);
	DECLARE @COMPRAS_ANTERIORES DECIMAL(18,0);
	DECLARE @VENTAS_ANTERIORES DECIMAL(18,0);
		
	SET @VENTAS_ANTERIORES = (SELECT SUM((ISNULL(CANTIDAD,0))) FROM [BREAKFAST_CLUB].BI_HECHOS_ACCESORIOS WHERE TIPO_TRANSACCION = 'Venta' AND (COD_TIEMPO BETWEEN 1 AND (@TIEMPO-1)) AND (COD_SUCURSAL = @SUCURSAL));

	SET @COMPRAS_ANTERIORES = (SELECT SUM((ISNULL(CANTIDAD,0))) FROM [BREAKFAST_CLUB].BI_HECHOS_ACCESORIOS	WHERE TIPO_TRANSACCION = 'Compra' AND (COD_TIEMPO BETWEEN 1 AND @TIEMPO) AND COD_SUCURSAL = @SUCURSAL);

	SET @RETURN = ISNULL(@COMPRAS_ANTERIORES, 0) - ISNULL(@VENTAS_ANTERIORES, 0) ;

	RETURN @RETURN
END
GO

CREATE FUNCTION [BREAKFAST_CLUB].STOCK_MAX_EN_ANIO(@SUCURSAL DECIMAL(18,0),@ANIO DECIMAL(18,0)) 
RETURNS DECIMAL(18,0)
AS 
BEGIN
    DECLARE @RET DECIMAL(18,0)

	DECLARE @PRIMER_MES DECIMAL (18,0) = (SELECT TOP 1 COD_TIEMPO FROM [BREAKFAST_CLUB].BI_TIEMPO WHERE ANIO = @ANIO ORDER BY MES ASC)
	DECLARE @ULTIMO_MES DECIMAL (18,0) = (SELECT TOP 1 COD_TIEMPO FROM [BREAKFAST_CLUB].BI_TIEMPO WHERE ANIO = @ANIO ORDER BY MES DESC)
	
	SET @RET = (SELECT MAX([BREAKFAST_CLUB].OBTENER_STOCK_DISPONIBLE_SUCURSAL_ACCESORIO(COD_TIEMPO, @SUCURSAL))
	FROM [BREAKFAST_CLUB].BI_HECHOS_ACCESORIOS WHERE COD_SUCURSAL = @SUCURSAL 
	AND COD_TIEMPO BETWEEN @PRIMER_MES AND @ULTIMO_MES
	GROUP BY COD_SUCURSAL) 

    RETURN @RET
END
GO

/* TIEMPO EN STOCK ACCESORIO */

CREATE FUNCTION [BREAKFAST_CLUB].DIFERENCIA_TIEMPO_ACCES_SUCURSAL(@COD_ACCESORIO DECIMAL(18,0), @COD_SUCURSAL DECIMAL(18,0))
RETURNS  DECIMAL(18,0) AS
BEGIN

	DECLARE @ventas TABLE (cant_ventas DECIMAL(18,0), tiempo DECIMAL(18,0))
	INSERT INTO @ventas	SELECT SUM(CANTIDAD), SUM(CANTIDAD * COD_TIEMPO) FROM [BREAKFAST_CLUB].BI_HECHOS_ACCESORIOS  
	WHERE TIPO_TRANSACCION = 'VENTA' AND COD_ACCESORIO = @COD_ACCESORIO AND COD_SUCURSAL = @COD_SUCURSAL

	DECLARE @CANT_TOTAL_VENTAS DECIMAL(18,0) = (select cant_ventas from @ventas)
	DECLARE @TIEMPO_VENTA DECIMAL(18,0) = (select tiempo from @ventas)

	DECLARE @MES_ANTERIOR DECIMAL(18,0) = (SELECT MAX(A.COD_TIEMPO) FROM (SELECT ha.cod_tiempo AS COD_TIEMPO FROM   [BREAKFAST_CLUB].BI_HECHOS_ACCESORIOS ha
	JOIN   [BREAKFAST_CLUB].BI_HECHOS_ACCESORIOS ha1 ON (ha1.COD_TIEMPO <= ha.cod_tiempo 
	and ha.COD_ACCESORIO = ha1.COD_ACCESORIO and ha.COD_SUCURSAL = ha1.COD_SUCURSAL and ha.TIPO_TRANSACCION = ha1.TIPO_TRANSACCION)
	WHERE  ha.CANTIDAD < @CANT_TOTAL_VENTAS and ha1.COD_ACCESORIO = @COD_ACCESORIO and ha1.COD_SUCURSAL = @COD_SUCURSAL	and ha1.TIPO_TRANSACCION = 'Compra'
	GROUP  BY ha.cod_tiempo	HAVING SUM(ha1.CANTIDAD) <  @CANT_TOTAL_VENTAS) A)

	DECLARE @compras_anteriores TABLE (cant_compras DECIMAL(18,0), tiempo_compras DECIMAL(18,0))
	INSERT INTO @compras_anteriores SELECT SUM(CANTIDAD), SUM(CANTIDAD * COD_TIEMPO) AS TOTAL FROM [BREAKFAST_CLUB].BI_HECHOS_ACCESORIOS 
	WHERE TIPO_TRANSACCION = 'COMPRA' AND COD_ACCESORIO = @COD_ACCESORIO AND COD_SUCURSAL = @COD_SUCURSAL AND COD_TIEMPO <= @MES_ANTERIOR

	DECLARE @TIEMPO_COMPRA DECIMAL(18,0) = (select tiempo_compras from @compras_anteriores)
	DECLARE @CANT_TOTAL_MES_ANTERIOR DECIMAL(18,0) = (select cant_compras from @compras_anteriores) 

	SET @TIEMPO_COMPRA = @TIEMPO_COMPRA + (@CANT_TOTAL_VENTAS - @CANT_TOTAL_MES_ANTERIOR) * (@MES_ANTERIOR + 1)
	 
	DECLARE @RETORNO DECIMAL(18,0) = @TIEMPO_VENTA - @TIEMPO_COMPRA

	RETURN @RETORNO
END
GO

CREATE FUNCTION [BREAKFAST_CLUB].TIEMPO_PROMEDIO_EN_STOCK_ACCESORIO(@COD_ACCESORIO DECIMAL(18,0))
RETURNS  DECIMAL(18,4) AS
BEGIN
	DECLARE @RETORNO DECIMAL(18,4) = (SELECT SUM([BREAKFAST_CLUB].DIFERENCIA_TIEMPO_ACCES_SUCURSAL(@COD_ACCESORIO , COD_SUCURSAL))	
									FROM [BREAKFAST_CLUB].BI_SUCURSAL)

	DECLARE @CANT_TOTAL_VENTAS DECIMAL(18,4) = (SELECT SUM(CANTIDAD) FROM [BREAKFAST_CLUB].BI_HECHOS_ACCESORIOS  
	WHERE TIPO_TRANSACCION = 'VENTA' AND COD_ACCESORIO = @COD_ACCESORIO)

	SET @RETORNO = @RETORNO/@CANT_TOTAL_VENTAS

	RETURN @RETORNO
END						
GO

/* TIEMPO EN STOCK PC */

CREATE FUNCTION [BREAKFAST_CLUB].DIFERENCIA_TIEMPO_PC_SUCURSAL(@COD_PC nvarchar(50), @COD_SUCURSAL DECIMAL(18,0))
RETURNS  DECIMAL(18,0) AS
BEGIN

	DECLARE @ventas TABLE (cant_ventas DECIMAL(18,0), tiempo DECIMAL(18,0))
	INSERT INTO @ventas	SELECT SUM(CANTIDAD), SUM(CANTIDAD * COD_TIEMPO) FROM [BREAKFAST_CLUB].BI_HECHOS_PC  
	WHERE TIPO_TRANSACCION = 'VENTA' AND COD_PC = @COD_PC AND COD_SUCURSAL = @COD_SUCURSAL

	DECLARE @CANT_TOTAL_VENTAS DECIMAL(18,0) = (select cant_ventas from @ventas)
	DECLARE @TIEMPO_VENTA DECIMAL(18,0) = (select tiempo from @ventas)

	DECLARE @MES_ANTERIOR DECIMAL(18,0) = (SELECT MAX(A.COD_TIEMPO) FROM (SELECT ha.cod_tiempo AS COD_TIEMPO FROM   [BREAKFAST_CLUB].BI_HECHOS_PC ha
	JOIN   [BREAKFAST_CLUB].BI_HECHOS_PC ha1 ON (ha1.COD_TIEMPO <= ha.cod_tiempo 
	and ha.COD_PC = ha1.COD_PC and ha.COD_SUCURSAL = ha1.COD_SUCURSAL and ha.TIPO_TRANSACCION = ha1.TIPO_TRANSACCION)
	WHERE  ha.CANTIDAD < @CANT_TOTAL_VENTAS and ha1.COD_PC = @COD_PC and ha1.COD_SUCURSAL = @COD_SUCURSAL	and ha1.TIPO_TRANSACCION = 'Compra'
	GROUP  BY ha.cod_tiempo	HAVING SUM(ha1.CANTIDAD) <  @CANT_TOTAL_VENTAS) A)

	DECLARE @compras_anteriores TABLE (cant_compras DECIMAL(18,0), tiempo_compras DECIMAL(18,0))
	INSERT INTO @compras_anteriores SELECT SUM(CANTIDAD), SUM(CANTIDAD * COD_TIEMPO) AS TOTAL FROM [BREAKFAST_CLUB].BI_HECHOS_PC 
	WHERE TIPO_TRANSACCION = 'COMPRA' AND COD_PC = @COD_PC AND COD_SUCURSAL = @COD_SUCURSAL AND COD_TIEMPO <= @MES_ANTERIOR

	DECLARE @TIEMPO_COMPRA DECIMAL(18,0) = (select tiempo_compras from @compras_anteriores)
	DECLARE @CANT_TOTAL_MES_ANTERIOR DECIMAL(18,0) = (select cant_compras from @compras_anteriores) 

	SET @TIEMPO_COMPRA = @TIEMPO_COMPRA + (@CANT_TOTAL_VENTAS - @CANT_TOTAL_MES_ANTERIOR) * (@MES_ANTERIOR + 1)
	 
	DECLARE @RETORNO DECIMAL(18,0) = @TIEMPO_VENTA - @TIEMPO_COMPRA

	RETURN @RETORNO

END
GO

CREATE FUNCTION [BREAKFAST_CLUB].TIEMPO_PROMEDIO_EN_STOCK_PC(@COD_PC nvarchar(50))
RETURNS  DECIMAL(18,4) AS
BEGIN
	DECLARE @RETORNO DECIMAL(18,4) = (SELECT SUM([BREAKFAST_CLUB].DIFERENCIA_TIEMPO_PC_SUCURSAL(@COD_PC , COD_SUCURSAL))	
									FROM [BREAKFAST_CLUB].BI_SUCURSAL)

	DECLARE @CANT_TOTAL_VENTAS DECIMAL(18,4) = (SELECT SUM(CANTIDAD) FROM [BREAKFAST_CLUB].BI_HECHOS_PC  
	WHERE TIPO_TRANSACCION = 'VENTA' AND COD_PC = @COD_PC)

	SET @RETORNO = @RETORNO/@CANT_TOTAL_VENTAS

	RETURN @RETORNO
END						
GO

/* PROCEDURES */

/* CLIENTE */

CREATE PROCEDURE [BREAKFAST_CLUB].PRC_INSERT_BI_CLIENTE
AS
BEGIN TRANSACTION
INSERT INTO [BREAKFAST_CLUB].[BI_CLIENTE] SELECT COD_CLIENTE, NOMBRE, APELLIDO, DNI, 
[BREAKFAST_CLUB].OBTENER_RANGO_EDAD(FECHA_NAC), MAIL, TELEFONO, DIRECCION 
FROM [BREAKFAST_CLUB].[CLIENTE]

COMMIT;
GO

/* TIEMPO */

CREATE PROCEDURE [BREAKFAST_CLUB].PRC_INSERT_BI_TIEMPO
AS
BEGIN TRANSACTION

INSERT INTO [BREAKFAST_CLUB].[BI_Tiempo] 
SELECT DISTINCT *
FROM 
(SELECT YEAR (F.FECHA) AS ANIO  , MONTH(F.FECHA) AS MES
FROM [BREAKFAST_CLUB].FACTURA F
UNION 
SELECT YEAR (C.FECHA) AS ANIO  , MONTH(C.FECHA) AS MES 
FROM [BREAKFAST_CLUB].COMPRA C) A ORDER BY ANIO, MES ASC

COMMIT;
GO

/* SUCURSAL */

CREATE PROCEDURE [BREAKFAST_CLUB].PRC_INSERT_BI_SUCURSAL
AS
BEGIN TRANSACTION
INSERT INTO [BREAKFAST_CLUB].[BI_SUCURSAL]
SELECT * FROM [BREAKFAST_CLUB].[SUCURSAL]

COMMIT;
GO

/* PC */

CREATE PROCEDURE [BREAKFAST_CLUB].PRC_INSERT_BI_PC
AS
BEGIN TRANSACTION
INSERT INTO [BREAKFAST_CLUB].[BI_PC]
SELECT COD_PC, ALTO, ANCHO, PROFUNDIDAD FROM [BREAKFAST_CLUB].[PC]

COMMIT;
GO

/* FABRICANTE */

CREATE PROCEDURE [BREAKFAST_CLUB].PRC_INSERT_BI_FABRICANTE
AS
BEGIN TRANSACTION
INSERT INTO [BREAKFAST_CLUB].[BI_FABRICANTE]
SELECT * FROM [BREAKFAST_CLUB].[FABRICANTE]

COMMIT;
GO
	
/* GPU */

CREATE PROCEDURE [BREAKFAST_CLUB].PRC_INSERT_BI_GPU
AS
BEGIN TRANSACTION
INSERT INTO [BREAKFAST_CLUB].[BI_GPU]
SELECT * FROM [BREAKFAST_CLUB].[GPU]

COMMIT;
GO
   
/* CPU */

CREATE PROCEDURE [BREAKFAST_CLUB].PRC_INSERT_BI_CPU
AS
BEGIN TRANSACTION
INSERT INTO [BREAKFAST_CLUB].[BI_CPU]
SELECT * FROM [BREAKFAST_CLUB].[CPU]

COMMIT;
GO
	
/* DISCO RIGIDO */

CREATE PROCEDURE [BREAKFAST_CLUB].PRC_INSERT_BI_DISCO_RIGIDO
AS
BEGIN TRANSACTION
INSERT INTO [BREAKFAST_CLUB].[BI_DISCO_RIGIDO]
SELECT * FROM [BREAKFAST_CLUB].[DISCO_RIGIDO]

COMMIT;
GO

/* RAM */
    
CREATE PROCEDURE [BREAKFAST_CLUB].PRC_INSERT_BI_RAM
AS
BEGIN TRANSACTION
INSERT INTO [BREAKFAST_CLUB].[BI_RAM]
SELECT * FROM [BREAKFAST_CLUB].[RAM]

COMMIT;
GO
    
/* ACCESORIO */

CREATE PROCEDURE [BREAKFAST_CLUB].PRC_INSERT_BI_ACCESORIO
AS
BEGIN TRANSACTION
INSERT INTO [BREAKFAST_CLUB].[BI_ACCESORIO]
SELECT * FROM [BREAKFAST_CLUB].[ACCESORIO]

COMMIT;
GO

/* HECHOS ACCESORIOS */

CREATE PROCEDURE [BREAKFAST_CLUB].PRC_INSERT_BI_HECHOS_ACCESORIOS
AS
BEGIN TRANSACTION

	/*COMPRAS ACCESORIOS POR SUCURSAL POR MES */

	BEGIN TRANSACTION
	INSERT INTO [BREAKFAST_CLUB].[BI_HECHOS_ACCESORIOS]
	SELECT 'Compra', AxC.COD_ACCES, S.COD_SUCURSAL, [BREAKFAST_CLUB].OBTENER_CODIGO_TIEMPO(C.FECHA) AS COD_TIEMPO, AxC.PRECIO, SUM(CANTIDAD) AS CANTIDAD, NULL
	FROM [BREAKFAST_CLUB].ACCESORIO_X_COMPRA AxC 
	INNER JOIN [BREAKFAST_CLUB].COMPRA C ON AxC.NRO_COMPRA = C.NRO_COMPRA
	INNER JOIN [BREAKFAST_CLUB].SUCURSAL S ON S.COD_SUCURSAL = C.COD_SUCURSAL
	GROUP BY C.FECHA, AxC.COD_ACCES, S.COD_SUCURSAL, AxC.PRECIO
	ORDER BY C.FECHA
	COMMIT;

	/*VENTAS ACCESORIOS POR SUCURSAL POR CLIENTE POR MES*/

	BEGIN TRANSACTION
	INSERT INTO [BREAKFAST_CLUB].[BI_HECHOS_ACCESORIOS]
	SELECT 'Venta', AxF.COD_ACCES, S.COD_SUCURSAL, [BREAKFAST_CLUB].OBTENER_CODIGO_TIEMPO(F.FECHA) AS COD_TIEMPO, AxF.PRECIO, SUM(CANTIDAD) AS CANT, COD_CLIENTE
	FROM [BREAKFAST_CLUB].ACCESORIO_X_FACTURA AxF
	INNER JOIN [BREAKFAST_CLUB].FACTURA F ON AxF.NRO_FACTURA = F.NRO_FACTURA
	INNER JOIN [BREAKFAST_CLUB].SUCURSAL S ON S.COD_SUCURSAL = F.COD_SUCURSAL
	GROUP BY S.COD_SUCURSAL, COD_CLIENTE,  AxF.COD_ACCES, AxF.PRECIO, F.FECHA
	COMMIT;

COMMIT;
GO

/* HECHOS PC */

CREATE PROCEDURE [BREAKFAST_CLUB].PRC_INSERT_BI_HECHOS_PC
AS
BEGIN TRANSACTION

	/* COMPRAS PC POR SUCURSAL POR MES */
	BEGIN TRANSACTION
	INSERT INTO [BREAKFAST_CLUB].[BI_HECHOS_PC]
	SELECT 'Compra', PxC.COD_PC, COD_RAM, COD_DISCO, COD_GPU, COD_CPU, S.COD_SUCURSAL, NULL, [BREAKFAST_CLUB].OBTENER_CODIGO_TIEMPO(C.FECHA) AS COD_TIEMPO, PxC.PRECIO, SUM(CANTIDAD) AS CANTIDAD
	FROM [BREAKFAST_CLUB].PC_X_COMPRA PxC 
	INNER JOIN [BREAKFAST_CLUB].COMPRA C ON PxC.NRO_COMPRA = C.NRO_COMPRA
	INNER JOIN [BREAKFAST_CLUB].SUCURSAL S ON S.COD_SUCURSAL = C.COD_SUCURSAL
	INNER JOIN [BREAKFAST_CLUB].PC PC ON PC.COD_PC = PxC.COD_PC
	GROUP BY C.FECHA, PxC.COD_PC, COD_GPU,  COD_RAM, COD_DISCO, COD_CPU, S.COD_SUCURSAL, PxC.PRECIO
	ORDER BY C.FECHA
	COMMIT;

	/* VENTAS PC POR SUCURSAL POR CLIENTE POR MES */
	BEGIN TRANSACTION
	INSERT INTO [BREAKFAST_CLUB].[BI_HECHOS_PC] 
	SELECT 'Venta', PxF.COD_PC, COD_RAM, COD_DISCO, COD_GPU, COD_CPU, S.COD_SUCURSAL, F.COD_CLIENTE, [BREAKFAST_CLUB].OBTENER_CODIGO_TIEMPO(F.FECHA) AS COD_TIEMPO, PxF.PRECIO, SUM(CANTIDAD) AS CANTIDAD
	FROM [BREAKFAST_CLUB].PC_X_FACTURA PxF 
	INNER JOIN [BREAKFAST_CLUB].FACTURA F ON PxF.NRO_FACTURA = F.NRO_FACTURA
	INNER JOIN [BREAKFAST_CLUB].SUCURSAL S ON S.COD_SUCURSAL = F.COD_SUCURSAL
	INNER JOIN [BREAKFAST_CLUB].PC PC ON PC.COD_PC = PxF.COD_PC
	GROUP BY F.FECHA, PxF.COD_PC, COD_GPU,  COD_RAM, COD_DISCO, COD_CPU, S.COD_SUCURSAL, F.COD_CLIENTE, PxF.PRECIO
	ORDER BY F.FECHA
	COMMIT;


COMMIT;
GO

/* VISTAS */

/* VISTAS PC */
/* Promedio de tiempo en stock de cada modelo de Pc */
CREATE VIEW [BREAKFAST_CLUB].BI_PROMEDIO_TIEMPO_EN_STOCK_PC AS
SELECT COD_PC, [BREAKFAST_CLUB].TIEMPO_PROMEDIO_EN_STOCK_PC(COD_PC) AS TIEMPO_EN_STOCK_PROMEDIO_EN_MESES
FROM [BREAKFAST_CLUB].BI_PC
GROUP BY COD_PC
GO


/* Precio promedio de PCs, vendidos y comprados */
CREATE VIEW [BREAKFAST_CLUB].BI_PRECIO_PROMEDIO_PC AS
SELECT T.COD_PC, (SELECT ROUND(CAST(AVG(PRECIO) AS decimal(18,1)),1) 
FROM [BREAKFAST_CLUB].BI_HECHOS_PC C WHERE TIPO_TRANSACCION = 'Compra' AND C.COD_PC = T.COD_PC) AS PRECIO_PROMEDIO_COMPRA, 
(SELECT ROUND(CAST(AVG(PRECIO) AS decimal(18,1)),1) 
FROM [BREAKFAST_CLUB].BI_HECHOS_PC V WHERE TIPO_TRANSACCION = 'Venta' AND V.COD_PC = T.COD_PC) AS PRECIO_PROMEDIO_VENTA
FROM [BREAKFAST_CLUB].BI_HECHOS_PC T GROUP BY T.COD_PC
GO

/* Cantidad de PCs, vendidos y comprados x sucursal y mes */
CREATE VIEW [BREAKFAST_CLUB].BI_CANT_COMPRAS_VENTAS_PC_X_SUCURSAL_X_MES AS
SELECT T.COD_PC, T.COD_SUCURSAL, MES, ANIO , ISNULL((SELECT SUM(CANTIDAD)  
FROM [BREAKFAST_CLUB].BI_HECHOS_PC C WHERE TIPO_TRANSACCION = 'Compra' 
AND C.COD_PC = T.COD_PC AND C.COD_SUCURSAL = T.COD_SUCURSAL AND C.COD_TIEMPO = T.COD_TIEMPO),0) AS CANTIDAD_COMPRAS, 
ISNULL((SELECT SUM(CANTIDAD) FROM [BREAKFAST_CLUB].BI_HECHOS_PC C WHERE TIPO_TRANSACCION = 'Venta' 
AND C.COD_PC = T.COD_PC AND C.COD_SUCURSAL = T.COD_SUCURSAL AND C.COD_TIEMPO = T.COD_TIEMPO),0) AS CANTIDAD_VENTAS 
FROM [BREAKFAST_CLUB].BI_HECHOS_PC T JOIN [BREAKFAST_CLUB].BI_TIEMPO TI ON T.COD_TIEMPO = TI.COD_TIEMPO GROUP BY T.COD_PC, T.COD_SUCURSAL, MES, ANIO, T.COD_TIEMPO
GO
 
/* Ganancias (precio de venta – precio de compra) x Sucursal x mes */
CREATE VIEW [BREAKFAST_CLUB].BI_GANANCIAS_PC_X_SUCURSAL_X_MES AS
SELECT T.COD_SUCURSAL, MES, ANIO, ISNULL((SELECT SUM(ISNULL(PRECIO,0) * ISNULL(CANTIDAD,0)) FROM [BREAKFAST_CLUB].BI_HECHOS_PC V 
WHERE TIPO_TRANSACCION = 'Venta'  AND V.COD_TIEMPO = T.COD_TIEMPO AND V.COD_SUCURSAL = T.COD_SUCURSAL GROUP BY V.COD_TIEMPO),0) - ISNULL((SELECT SUM(ISNULL(PRECIO,0) * ISNULL(CANTIDAD,0)) 
FROM [BREAKFAST_CLUB].BI_HECHOS_PC C WHERE TIPO_TRANSACCION = 'Compra' AND C.COD_TIEMPO = T.COD_TIEMPO AND C.COD_SUCURSAL = T.COD_SUCURSAL
GROUP BY C.COD_TIEMPO),0) AS GANANCIA 
FROM [BREAKFAST_CLUB].BI_HECHOS_PC T JOIN [BREAKFAST_CLUB].BI_TIEMPO TI ON T.COD_TIEMPO = TI.COD_TIEMPO 
GROUP BY T.COD_SUCURSAL, MES, ANIO, T.COD_TIEMPO
GO

/* VISTAS ACCESORIOS */
/* Precio promedio de cada accesorio, vendido y comprado */
CREATE VIEW [BREAKFAST_CLUB].BI_PRECIO_PROMEDIO_ACCESORIO AS
SELECT T.COD_ACCESORIO, (SELECT ROUND(CAST(AVG(PRECIO) AS decimal(18,1)),1) 
FROM [BREAKFAST_CLUB].BI_HECHOS_ACCESORIOS C WHERE TIPO_TRANSACCION = 'Compra' AND C.COD_ACCESORIO = T.COD_ACCESORIO) AS PRECIO_PROMEDIO_COMPRA, 
(SELECT ROUND(CAST(AVG(PRECIO) AS decimal(18,1)),1) 
FROM [BREAKFAST_CLUB].BI_HECHOS_ACCESORIOS V WHERE TIPO_TRANSACCION = 'Venta' AND V.COD_ACCESORIO = T.COD_ACCESORIO) AS PRECIO_PROMEDIO_VENTA
FROM [BREAKFAST_CLUB].BI_HECHOS_ACCESORIOS T GROUP BY T.COD_ACCESORIO
GO

/* Ganancias (precio de venta – precio de compra) x Sucursal x mes */
CREATE VIEW [BREAKFAST_CLUB].BI_GANANCIAS_ACCESORIO_X_SUCURSAL_X_MES AS
SELECT T.COD_SUCURSAL, MES, ANIO, 
ISNULL((SELECT SUM(ISNULL(PRECIO,0) * ISNULL(CANTIDAD,0)) FROM [BREAKFAST_CLUB].BI_HECHOS_ACCESORIOS V 
WHERE TIPO_TRANSACCION = 'Venta'  AND V.COD_TIEMPO = T.COD_TIEMPO AND V.COD_SUCURSAL = T.COD_SUCURSAL GROUP BY V.COD_TIEMPO),0) - 
ISNULL((SELECT SUM(ISNULL(PRECIO,0) * ISNULL(CANTIDAD,0)) FROM [BREAKFAST_CLUB].BI_HECHOS_ACCESORIOS C 
WHERE TIPO_TRANSACCION = 'Compra' AND C.COD_TIEMPO = T.COD_TIEMPO AND C.COD_SUCURSAL = T.COD_SUCURSAL GROUP BY C.COD_TIEMPO),0) AS GANANCIA 
FROM [BREAKFAST_CLUB].BI_HECHOS_ACCESORIOS T JOIN [BREAKFAST_CLUB].BI_TIEMPO TI ON T.COD_TIEMPO = TI.COD_TIEMPO 
GROUP BY T.COD_SUCURSAL, MES, ANIO, T.COD_TIEMPO
GO

/* Promedio de tiempo en stock de cada modelo de accesorio */
CREATE VIEW [BREAKFAST_CLUB].BI_PROMEDIO_TIEMPO_EN_STOCK_ACCESORIO AS
SELECT COD_ACCES, [BREAKFAST_CLUB].TIEMPO_PROMEDIO_EN_STOCK_ACCESORIO(COD_ACCES) AS TIEMPO_EN_STOCK_PROMEDIO_EN_MESES
FROM [BREAKFAST_CLUB].BI_ACCESORIO
GROUP BY COD_ACCES
GO

/* Máxima cantidad de stock por cada sucursal (anual) */
CREATE VIEW [BREAKFAST_CLUB].BI_CANT_MAXIMA_EN_STOCK_X_SUCURSAL_X_ANIO AS
SELECT COD_SUCURSAL, ANIO, [BREAKFAST_CLUB].STOCK_MAX_EN_ANIO(COD_SUCURSAL,ANIO) 'STOCK MAXIMO'
FROM [BREAKFAST_CLUB].BI_HECHOS_ACCESORIOS A JOIN [BREAKFAST_CLUB].BI_TIEMPO T ON A.COD_TIEMPO = T.COD_TIEMPO
GROUP BY COD_SUCURSAL, ANIO
GO

EXEC [BREAKFAST_CLUB].PRC_INSERT_BI_TIEMPO
EXEC [BREAKFAST_CLUB].PRC_INSERT_BI_FABRICANTE
EXEC [BREAKFAST_CLUB].PRC_INSERT_BI_CLIENTE
EXEC [BREAKFAST_CLUB].PRC_INSERT_BI_SUCURSAL
EXEC [BREAKFAST_CLUB].PRC_INSERT_BI_PC
EXEC [BREAKFAST_CLUB].PRC_INSERT_BI_GPU
EXEC [BREAKFAST_CLUB].PRC_INSERT_BI_CPU
EXEC [BREAKFAST_CLUB].PRC_INSERT_BI_DISCO_RIGIDO
EXEC [BREAKFAST_CLUB].PRC_INSERT_BI_RAM
EXEC [BREAKFAST_CLUB].PRC_INSERT_BI_ACCESORIO

EXEC [BREAKFAST_CLUB].PRC_INSERT_BI_HECHOS_ACCESORIOS
EXEC [BREAKFAST_CLUB].PRC_INSERT_BI_HECHOS_PC


GO